group = 'com.taobao.android.atlasdemo'
version = getEnvValue("versionName", "1.0.0");
def apVersion = getEnvValue("apVersion", "");

apply plugin: 'com.android.application'
apply plugin: 'com.taobao.atlas'
apply plugin: 'com.taobao.atlas.dexpatch'
apply from: 'dexPatchWraper.gradle'

//通过增加判断逻辑，打出不同类型的定制包
def appId = "com.unit.test.myatlastest"
def minVersion = 14
if (project.hasProperty("beta")) {
    appId = "com.taobao.atlas.beta"
    minVersion = 21
}

repositories {
    mavenLocal()
    jcenter()
}

android {
    signingConfigs {
        config {
            keyAlias 'key0'
            keyPassword '123456'
            storeFile file('/Users/yangsimin/Documents/code/MyAtlasTest2/atlastest.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId appId
        minSdkVersion minVersion
        targetSdkVersion 25
        versionCode 1
        versionName version
        vectorDrawables.useSupportLibrary = true
        //通过增加判断逻辑，打出不同类型的定制包
        if (project.hasProperty("beta")) {
            buildConfigField "boolean", "API_ENV", "false"
        } else {
            buildConfigField "boolean", "API_ENV", "false"
        }
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            ndk {
                abiFilters "x86", "armeabi"
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            ndk {
                abiFilters "x86", "armeabi"
            }
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        doNotStrip '*/mips/*.so'
        doNotStrip '*/mips64/*.so'
    }
    //    productFlavors {
    //        dev {
    //            minSdkVersion 14
    //        }
    //        beta {
    //            minSdkVersion 21
    //        }
    //    }
}

atlas {

    atlasEnabled true
    tBuildConfig {
        autoStartBundles = ['com.taobao.firstbundle'] //自启动bundle配置
        outOfApkBundles = ['remotebundle']
        preLaunch = 'com.unit.test.myatlastest.DemoPreLaunch'
        classInject false
        dataBindingBundles = ['com.taobao.databindbundle']
    }

    manifestOptions {
        addAtlasProxyComponents true
    }


    patchConfigs {
        debug {
            createTPatch true
        }
    }


    buildTypes {
        debug {
            if (apVersion) {
                baseApDependency "com.taobao.android.atlasdemo:AP-debug:${apVersion}@ap"
                patchConfig patchConfigs.debug
            }
        }
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.android.support:support-vector-drawable:25.1.0'
    compile 'com.android.support:support-v4:25.1.0'
    compile rootProject.ext.lib_atlas_update
    compile 'com.alibaba:fastjson:1.1.45.android@jar'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'
    compile 'com.android.support:recyclerview-v7:25.1.0'
    //    compile project(':atlas_core')
    //    bundleCompile project(':firstbundle')
    compile project(':firstbundle')
    compile project(':activitygroupcompat')
    //依赖atlas-core引擎代码组件
    compile 'com.taobao.android:atlas_core:5.0.7.55@aar'
    //依赖atlas-update更新代码组件
    compile 'com.taobao.android:atlasupdate:1.1.4.14@aar'
}

String getEnvValue(key, defValue) {
    def val = System.getProperty(key);
    if (null != val) {
        return val;
    }
    val = System.getenv(key);
    if (null != val) {
        return val;
    }
    return defValue;
}

tasks.whenTaskAdded { task ->
    if (task.name.contains("DebugAndroidTest")) {
        task.setEnabled(false);
    }
    if (task.name.contains("assemble")) {
        def files = null;
        def file = new File(task.project.getBuildDir(), "outputs");
        if (file.exists() && new File(file, "tpatch-debug").exists()) {
            files = new File(file, "tpatch-debug").listFiles();
        }
        if (files != null) {
            for (File file1 : files) {
                if (file1.getName().endsWith(".json") || file1.getName().endsWith(".tpatch")) {

                    if (!new File(task.project.getRootDir(), "hisTpatch").exists()) {
                        new File(task.project.getRootDir(), "hisTpatch").mkdirs();
                    }
                    org.apache.commons.io.FileUtils.copyFileToDirectory(file1, new File(task.project.getRootDir(), "hisTpatch"));
                }
            }
        }
    }
}

apply plugin: 'maven'
apply plugin: 'maven-publish'

publishing {
    repositories {
        mavenLocal()
    }
}

publishing {
    publications {
//        maven(MavenPublication) {
//            artifact "${project.buildDir}/outputs/apk/${project.name}-debug.ap"
//            artifactId "AP-debug"
//        }

        maven(MavenPublication) {
            artifact "${project.buildDir}/outputs/apk/${project.name}-release.ap"
            artifactId "AP-debug"
        }
    }
}

